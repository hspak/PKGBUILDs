# Maintainer: Hong Shick Pak <hongshick.pak@gmail.com>

pkgname=tlclient
pkgver=4.1.1_4137
pkgrel=4
pkgdesc="ThinLinc client software for remote desktop"
arch=('x86_64')
url="https://www.cendio.com/products/thinlinc/"
license=('custom')
depends=('libx11' 'alsa-lib' 'zlib')
source=(http://www.cendio.com/downloads/clients/tl-${pkgver//_/-}-client-linux-dynamic-x86_64.tar.gz)
md5sums=(1b34edec54f2975f83682d2d4a1b0bbc)

package() {
  basedir="tl-${pkgver//_/-}-client-linux-dynamic-x86_64"
  mkdir -p "$pkgdir/usr"
  cp -dpr --no-preserve=ownership "$basedir/bin" "$pkgdir/usr/bin"
  cp -dpr --no-preserve=ownership "$basedir/lib" "$pkgdir/usr/lib"

  # config
  install -m644 -D "$basedir/etc/tlclient.conf" "$pkgdir/etc/tlclient.conf"

  # list of demo servers
  install -m644 -D "$basedir/etc/ssh_known_hosts" "$pkgdir/usr/share/doc/tlclient/known_hosts"

  # copy license into proper directory
  install -m644 -D "$basedir/lib/tlclient/EULA.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
