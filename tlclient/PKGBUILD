# Maintainer: Hong Shick Pak <hong@hspak.com>

pkgname=tlclient
pkgver=4.2.0.4356
pkgrel=1
pkgdesc="ThinLinc client software for remote desktop sessions"
arch=('x86_64')
url="https://www.cendio.com/products/thinlinc/"
license=('custom')
depends=('libx11' 'alsa-lib' 'zlib')
source=(http://www.cendio.com/downloads/clients/tl-${pkgver//0./0-}-client-linux-dynamic-x86_64.tar.gz)
md5sums=(85b683c9dff8b96e98e159a78f4fcb7b)

package() {
  cd "$srcdir/tl-${pkgver//0./0-}-client-linux-dynamic-x86_64"

  mkdir -p "$pkgdir/usr"
  cp -dpr --no-preserve=ownership bin "$pkgdir/usr/bin"
  cp -dpr --no-preserve=ownership lib "$pkgdir/usr/lib"

  # config
  install -Dm644 etc/tlclient.conf "$pkgdir/etc/tlclient.conf"

  # just a list of demo servers, not worth polluting /etc by default
  install -Dm644 etc/ssh_known_hosts "$pkgdir/usr/share/doc/tlclient/ssh_known_hosts"

  # copy license to proper directory
  install -Dm644 lib/tlclient/EULA.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
