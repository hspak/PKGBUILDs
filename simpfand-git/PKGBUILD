# Maintainer: Hong Shick Pak <hong@hspak.com>

pkgname=simpfand-git
pkgver=v3.1
pkgrel=1
pkgdesc="A simple fan control daemon for ThinkPads"
arch=('i686' 'x86_64')
url="http://github.com/hspasta/simpfand"
license=('MIT')
depends=()
makedepends=('git')
conflicts=('simpfand')
provides=('simpfand')
source=("git://github.com/hspasta/simpfand")
install=simpfand-git.install
md5sums=('SKIP')

pkgver() {
  cd 'simpfand'
  git describe --tags | sed 's/-/./g'
}

build() {
  make -C simpfand
}

package() {
  make -C simpfand PREFIX=/usr DESTDIR="$pkgdir" install
  install -D -m644 "${srcdir}/simpfand/simpfand.systemd" "${pkgdir}/usr/lib/systemd/system/simpfand.service"
  install -D -m755 "${srcdir}/simpfand/restart-simpfand.sh" "${pkgdir}/usr/lib/systemd/system-sleep/restart-simpfand.sh"

  # license
  install -D -m644 "${srcdir}/simpfand/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ft=sh syn=sh et
